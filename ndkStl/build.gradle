ext {
    buildToolsVersion = System.env.CUSTOM_BUILDTOOLS != null ? System.env.CUSTOM_BUILDTOOLS : '20.0.0'
}

buildscript {
    def gradleVersion = System.env.CUSTOM_GRADLE != null ? System.env.CUSTOM_GRADLE : '0.14.4'

    repositories {
        if (System.env.CUSTOM_REPO != null) {
            maven { url System.env.CUSTOM_REPO }
        } else {
            mavenCentral()
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
    }
}


// Create subproject with the same source directories.  Each subproject will use a different stl
// library.
subprojects {
    apply plugin: 'com.android.application'

    android {
        compileSdkVersion 19
        buildToolsVersion = rootProject.ext.buildToolsVersion

        useNewNativePlugin true
        ndk {
            moduleName "hello-jni"
        }

        buildTypes.debug.jniDebuggable true

        sourceSets {
            androidTest {
                java {
                    srcDir '../src/androidTest/java'
                }
            }
            main {
                manifest.srcFile '../src/main/AndroidManifest.xml'
                res {
                    srcDir '../src/main/res'
                }
                java {
                    srcDir '../src/main/java'
                }
                jni {
                    srcDir '../src/main/jni'
                }
            }
        }
    }
}
